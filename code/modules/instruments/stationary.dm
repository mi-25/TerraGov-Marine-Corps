/obj/structure/musician
	name = "不是钢琴"
	desc = "出现故障，请联系编码员。"
	interaction_flags = INTERACT_OBJ_DEFAULT
	var/can_play_unanchored = FALSE
	var/list/allowed_instrument_ids = list("r3grand","r3harpsi","crharpsi","crgrand1","crbright1", "crichugan", "crihamgan","piano")
	var/datum/song/song

/obj/structure/musician/Initialize(mapload)
	. = ..()
	song = new(src, allowed_instrument_ids)
	allowed_instrument_ids = null

/obj/structure/musician/Destroy()
	QDEL_NULL(song)
	return ..()

/obj/structure/musician/proc/should_stop_playing(mob/user)
	if(!(anchored || can_play_unanchored))
		return TRUE
	if(!user)
		return FALSE
	return !can_interact(user)

/obj/structure/musician/attack_hand(mob/living/user)
	. = ..()
	if(can_interact(user))
		interact(user)

/obj/structure/musician/ui_interact(mob/user)
	. = ..()
	song.ui_interact(user)

/obj/structure/musician/wrench_act(mob/living/user, obj/item/I)
	anchored = !anchored
	user.visible_message("[user] 解开了 [src]", "you unfasten the [src]")
	playsound(loc, 'sound/items/ratchet.ogg', 25, TRUE)
	return TRUE

/obj/structure/musician/piano
	name = "太空迷你穆格"
	icon = 'icons/obj/musician.dmi'
	icon_state = "minimoog"
	anchored = TRUE
	density = TRUE

/obj/structure/musician/piano/unanchored
	anchored = FALSE

/obj/structure/musician/piano/Initialize(mapload)
	. = ..()
	if(prob(50) && icon_state == initial(icon_state))
		name = "太空迷你穆格"
		desc = "这是一台迷你穆格合成器，就像太空钢琴，但更有太空感！"
		icon_state = "minimoog"
	else
		name = "太空钢琴"
		desc = "这是一架太空钢琴，就像普通钢琴一样，但永远音准！即使演奏者不准。"
		icon_state = "piano"
