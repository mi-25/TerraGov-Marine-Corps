/obj/item/attachable/reddot
	name = "红点瞄准镜"
	desc = "一款用于中短距离的红色瞄准镜。不具备变焦功能，但能显著提高瞄准时的武器精度和射速。\n无缺点。"
	icon_state = "reddot"
	icon = 'icons/obj/items/guns/attachments/rail.dmi'
	slot = ATTACHMENT_SLOT_RAIL
	accuracy_mod = 0.15
	accuracy_unwielded_mod = 0.1
	aim_mode_movement_mult = -0.35
	variants_by_parent_type = list(/obj/item/weapon/gun/rifle/som = "", /obj/item/weapon/gun/shotgun/som = "")

/obj/item/attachable/m16sight
	name = "M16机械瞄具"
	desc = "标志性的M16提把式机械瞄具。通常当用户找到值得安装在导轨上的配件时就会被拆下。"
	icon_state = "m16sight" // missing icon?
	icon = 'icons/obj/items/guns/attachments/rail.dmi'
	slot = ATTACHMENT_SLOT_RAIL
	accuracy_mod = 0.1
	accuracy_unwielded_mod = 0.05
	movement_acc_penalty_mod = -0.1

/obj/item/attachable/flashlight
	name = "导轨手电筒"
	desc = "一种简单的战术手电，用于安装在枪械上。\n除了提供光源外没有其他作用，但在提供光源方面也并非特别有用。"
	icon_state = "flashlight"
	icon = 'icons/obj/items/guns/attachments/rail.dmi'
	light_mod = 6
	light_system = MOVABLE_LIGHT
	slot = ATTACHMENT_SLOT_RAIL
	attach_features_flags = ATTACH_REMOVABLE|ATTACH_ACTIVATION
	attachment_action_type = /datum/action/item_action/toggle
	activation_sound = 'sound/items/flashlight.ogg'

/obj/item/attachable/flashlight/activate(mob/living/user, turn_off)
	turn_light(user, turn_off ? !turn_off : !light_on)

/obj/item/attachable/flashlight/turn_light(mob/user, toggle_on, cooldown, sparks, forced, light_again)
	. = ..()

	if(. != CHECKS_PASSED)
		return

	if(ismob(master_gun.loc) && !user)
		user = master_gun.loc

	if(!toggle_on && light_on)
		icon_state = initial(icon_state)
		light_on = FALSE
		master_gun.set_light_range(master_gun.light_range - light_mod)
		master_gun.set_light_power(master_gun.light_power - (light_mod * 0.5))
		if(master_gun.light_range <= 0) //does the gun have another light source
			master_gun.set_light_on(FALSE)
			REMOVE_TRAIT(master_gun, TRAIT_GUN_FLASHLIGHT_ON, GUN_TRAIT)
	else if(toggle_on & !light_on)
		icon_state = initial(icon_state) +"_on"
		light_on = TRUE
		master_gun.set_light_range(master_gun.light_range + light_mod)
		master_gun.set_light_power(master_gun.light_power + (light_mod * 0.5))
		if(!HAS_TRAIT(master_gun, TRAIT_GUN_FLASHLIGHT_ON))
			master_gun.set_light_on(TRUE)
			ADD_TRAIT(master_gun, TRAIT_GUN_FLASHLIGHT_ON, GUN_TRAIT)
	else
		return

	update_icon()

/obj/item/attachable/flashlight/attackby(obj/item/I, mob/user, params)
	. = ..()
	if(.)
		return

	if(istype(I,/obj/item/tool/screwdriver))
		to_chat(user, span_notice("你将导轨手电筒改回成普通手电筒。"))
		if(loc == user)
			user.temporarilyRemoveItemFromInventory(src)
		var/obj/item/flashlight/F = new(user)
		user.put_in_hands(F) //This proc tries right, left, then drops it all-in-one.
		qdel(src) //Delete da old flashlight

/obj/item/attachable/flashlight/under
	name = "下挂手电筒"
	desc = "一种简单的战术手电，用于安装在枪械上。\n除了提供光源外没有其他作用，但在提供光源方面也并非特别有用。"
	icon_state = "uflashlight"
	icon = 'icons/obj/items/guns/attachments/underbarrel.dmi'
	slot = ATTACHMENT_SLOT_UNDER
	attach_features_flags = ATTACH_REMOVABLE|ATTACH_ACTIVATION

/obj/item/attachable/quickfire
	name = "速射适配器"
	desc = "一种增强升级的自动装填机制，能够更快地发射子弹。\n然而，它也会降低精度和点射时发射的弹道数量。"
	slot = ATTACHMENT_SLOT_RAIL
	icon_state = "autoloader"
	icon = 'icons/obj/items/guns/attachments/rail.dmi'
	accuracy_mod = -0.10
	delay_mod = -0.125 SECONDS
	burst_mod = -1
	accuracy_unwielded_mod = -0.15

/obj/item/attachable/magnetic_harness
	name = "磁力背带"
	desc = "一种磁性连接的携行具套件，可安装在武器的导轨接口上。当武器掉落时，会通过吊带连接到TGMC护甲上。"
	icon_state = "magnetic"
	icon = 'icons/obj/items/guns/attachments/rail.dmi'
	slot = ATTACHMENT_SLOT_RAIL
	pixel_shift_x = 13
	///Handles the harness functionality, created when attached to a gun and removed on detach
	var/datum/component/reequip/reequip_component

/obj/item/attachable/magnetic_harness/on_attach(attaching_item, mob/user)
	. = ..()
	if(!master_gun)
		return
	reequip_component = master_gun.AddComponent(/datum/component/reequip, list(SLOT_S_STORE, SLOT_BELT, SLOT_BACK))

/obj/item/attachable/magnetic_harness/on_detach(attaching_item, mob/user)
	. = ..()
	if(master_gun)
		return
	QDEL_NULL(reequip_component)

/obj/item/attachable/buildasentry
	name = "\improper 哨戒炮组装枪械配件系统"
	icon = 'icons/obj/machines/deployable/sentry/build_a_sentry.dmi'
	icon_state = "build_a_sentry_attachment"
	desc = "Build-A-Sentry是廉价自动化防御的最新设计。只需将其安装在枪械的导轨上并部署即可。就是这么简单！"
	slot = ATTACHMENT_SLOT_RAIL
	size_mod = 1
	pixel_shift_x = 10
	pixel_shift_y = 18
	///Deploy time for the build-a-sentry
	var/deploy_time = 2 SECONDS
	///Undeploy tim for the build-a-sentry
	var/undeploy_time = 2 SECONDS

/obj/item/attachable/buildasentry/can_attach(obj/item/attaching_to, mob/attacher)
	if(!isgun(attaching_to))
		return FALSE
	var/obj/item/weapon/gun/attaching_gun = attaching_to
	if(ispath(attaching_gun.deployable_item, /obj/machinery/deployable/mounted/sentry))
		to_chat(attacher, span_warning("[attaching_gun] 已经是一台哨戒炮了！"))
		return FALSE
	return ..()

/obj/item/attachable/buildasentry/on_attach(attaching_item, mob/user)
	. = ..()
	ENABLE_BITFIELD(master_gun.item_flags, IS_DEPLOYABLE)
	master_gun.deployable_item = /obj/machinery/deployable/mounted/sentry/buildasentry
	master_gun.turret_flags |= TURRET_HAS_CAMERA|TURRET_SAFETY|TURRET_ALERTS
	master_gun.AddComponent(/datum/component/deployable_item, master_gun.deployable_item, deploy_time, undeploy_time)
	update_icon()

/obj/item/attachable/buildasentry/on_detach(detaching_item, mob/user)
	. = ..()
	var/obj/item/weapon/gun/detaching_gun = detaching_item
	DISABLE_BITFIELD(detaching_gun.item_flags, IS_DEPLOYABLE)
	qdel(detaching_gun.GetComponent(/datum/component/deployable_item))
	detaching_gun.deployable_item = null
	detaching_gun.turret_flags &= ~(TURRET_HAS_CAMERA|TURRET_SAFETY|TURRET_ALERTS)
